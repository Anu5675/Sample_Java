import java.nio.file.Files
import java.nio.file.Paths

apply plugin: 'java'

compileTestJava {
    sourceCompatibility = 8
    targetCompatibility = 8
}

task compileTestProbes {
    onlyIf {
        project.hasProperty("integration") &&
                new File("${projectDir}/../btrace-dist/build/resources/main/v${project.version}/libs/btrace-client.jar").exists()
    }
    dependsOn compileTestJava
    doLast {
        def clientJar = projectDir.toPath().resolve("../btrace-dist/build/resources/main/v${project.version}/libs/btrace-client.jar")
        def loader = new URLClassLoader(new URL[]{new URL("file://${clientJar}")})
        def compiler = loader.loadClass('org.openjdk.btrace.compiler.Compiler')
        def rtCp = sourceSets.test.runtimeClasspath

        def args = ["-cp", rtCp.plus(files(clientJar, buildDir.toPath().resolve("classes/java/test"), buildDir.toPath().resolve("classes/java/java11_dummy"))).getAsPath(), "-d", buildDir.toPath().resolve("classes")]

        def files = fileTree(dir: "src/test/btrace", include: '**/*.java', exclude: 'verifier/**/*.java').findAll {
            it != null
        }.collect { it }

        args.addAll(files)

        compiler.main(args as String[])
    }
}

task buildEventsJar(type: Jar) {
    inputs.files compileJava
    from files("${project.buildDir}/classes/java/main")
    archiveBaseName = "events"
    archiveVersion = ""
    archiveClassifier = ""
}

compileTestProbes.shouldRunAfter cleanTest
buildEventsJar.shouldRunAfter cleanTest

test {
    onlyIf {
        project.hasProperty("integration") &&
        new File("${projectDir}/../btrace-dist/build/resources/main/v${project.version}/libs/btrace-client.jar").exists()
    }
    dependsOn cleanTest, compileTestProbes, buildEventsJar

    testLogging.showStandardStreams = true

    def props = new Properties()
    props.load(Files.newInputStream(Paths.get(System.getenv("JAVA_HOME"), "release")))
    if (props.getProperty("JAVA_VERSION")?.contains("1.8")) {
        jvmArgs "-Dproject.dir=${projectDir}", "-Dproject.version=${project.version}", "-Dbtrace.libs=${projectDir}/../btrace-dist/build/resources/main/v${project.version}/libs/"
    } else {
        jvmArgs "-Dproject.dir=${projectDir}", "-Dbtrace.libs=${projectDir}/../btrace-dist/build/resources/main/v${project.version}/libs/", '-XX:+IgnoreUnrecognizedVMOptions', '--add-opens', 'java.base/jdk.internal.reflect=ALL-UNNAMED', '--add-exports', 'java.base/jdk.internal.reflect=ALL-UNNAMED', "-Dproject.version=${project.version}"
    }
}